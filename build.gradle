plugins {
    id "com.avast.gradle.docker-compose" version "0.9.4"
    id "com.palantir.jacoco-full-report" version "0.4.0"
}

allprojects {
    apply plugin: 'idea'

    repositories {
        mavenCentral()
    }
}

subprojects {
    if (project.name == 'tracker' || project.name == 'reporter') {
        apply plugin: 'java'
        apply plugin: 'jacoco'

        configurations {
            jacocoAgentRuntime
        }

        dependencies {
            jacocoAgentRuntime "org.jacoco:org.jacoco.agent:${jacoco.toolVersion}:runtime"
        }

        tasks.withType(Test) {
            useTestNG() {
                useDefaultListeners = true
            }

            testLogging {
                showStandardStreams = true
                events 'started', 'passed', 'skipped', 'failed'
            }

            outputs.upToDateWhen { false }

            dependsOn ':servicesComposeUp'
        }

        jacocoTestReport {
            dependsOn tasks.withType(Test)
            reports {
                xml.enabled = true
                html.enabled = true
            }
        }

        assemble.doLast {
            copy {
                from configurations.jacocoAgentRuntime[0]
                into "${buildDir}/libs"
                rename '.*jar', 'jacocoagent.jar'
            }
        }

        rootProject.tasks.getByName('composeUp') dependsOn assemble
    }
}

configurations { codacy }

dependencies {
    codacy 'com.codacy:codacy-coverage-reporter:+'
}

jacocoFullReport {
    executionData(fileTree(".").matching { include "**/*.exec" })
}

task sendCoverageToCodacy(type: JavaExec) {
    main = 'com.codacy.CodacyCoverageReporter'
    classpath = configurations.codacy
    args = ['report', '-l', 'Java', '-r', jacocoFullReport.reports.xml.destination.toString()]
    dependsOn jacocoFullReport
}

dockerCompose {
    upAdditionalArgs = ['--renew-anon-volumes']
    forceRecreate = true
    projectName = "expense-tracker"
    services {
        startedServices = ['tracker-db', 'reporter-db', 'mq']
        upAdditionalArgs = ['--renew-anon-volumes']
        forceRecreate = true
        projectName = "expense-tracker"
    }
}